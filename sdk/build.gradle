apply plugin: 'com.android.library'

def gitSha = 'git rev-parse HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 19
    buildToolsVersion "21.0.2"

    publishNonDefault true

    defaultConfig {
        applicationId "com.vodafone.global"
        minSdkVersion 15
        targetSdkVersion 19
        versionCode 1
        versionName "1.0.1"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        // If you want to enable direct connection to BE set DIRECT
        // to true and provide protocol and host.
        // If you want to change opco and msisdn do that in
        // com.vodafone.global.sdk.RequestBuilderProvider.builder
        buildConfigField "boolean", "DIRECT", "false"
        buildConfigField "String", "DIRECT_PROTOCOL", "\"\""
        buildConfigField "String", "DIRECT_HOST", "\"\""
    }
    buildTypes {
        debug {
            versionNameSuffix = "-DEBUG"
        }
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        preProd {
            buildConfigField "String", "CONFIG_URL", "\"https://preprod.appconfig.shared.sp.vodafone.com/seamless-id/v1/sdk-config-android/config.json\""
        }
        prod {
            buildConfigField "String", "CONFIG_URL", "\"https://appconfig.shared.sp.vodafone.com/seamless-id/v1/sdk-config-android/config.json\""
        }
        dit {
            buildConfigField "String", "CONFIG_URL", "\"https://preprod.appconfig.shared.sp.vodafone.com/seamless-id/v1/sdk-config-android/config.json\""
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            def flavorName = variant.flavorName
            // flavors can't have hyphen in their name so we have to change it
            if (flavorName == "preProd")
                flavorName = "pre-prod"
            def newName = file.name.replace("sdk-${variant.flavorName}", "vodafone-android-sdk-${flavorName}")
            if (variant.buildType.name == "release")
                newName = newName.replace("-release", "")
            output.outputFile = new File(file.parentFile, newName)
        }
    }
}

dependencies {
    // SDK is using SNAPSHOT (rev. c3a31993cea76b68d6a9a5fef4036576eb27b98b)
    // of OkHttp from `libs` because we need custom handling of http
    // redirection that isn't available in current release.
    // It should change to release version as soon as possible.
    // compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.google.guava:guava:17.0'
    compile 'joda-time:joda-time:2.4'
//    compile 'com.octo.android.robospice:robospice:1.4.14'
//    compile ('com.octo.android.robospice:robospice-okhttp:1.4.14') {
//        exclude group: 'com.squareup.okhttp'
//    }
    compile 'com.squareup.okio:okio:1.0.1'
    debugCompile 'com.jakewharton.timber:timber:+'
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

task zip2jar(type: Zip, dependsOn: "compileDitReleaseJava") {
    from 'build/intermediates/classes/dit/release'
    destinationDir = file('build/libs')
    extension = "jar"
}

task createPom {
    apply plugin: 'maven'
    description "Generates pom.xml"
    pom {
        project {
            groupId 'com.vodafone.global'
            artifactId 'android-sdk'
            version '1.0.1'
            packaging 'aar'
        }
    }.withXml {
        def dependenciesNode = asNode().appendNode('dependencies')

        configurations.compile.allDependencies.each { dependency ->
            def dependencyNode = dependenciesNode.appendNode('dependency')
            dependencyNode.appendNode('groupId', dependency.group)
            dependencyNode.appendNode('artifactId', dependency.name)
            dependencyNode.appendNode('version', dependency.version)
        }
    }.writeTo("$buildDir/pom.xml")
}
