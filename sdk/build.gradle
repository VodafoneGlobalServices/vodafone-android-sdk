apply plugin: 'com.android.library'

def gitSha = 'git rev-parse HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    publishNonDefault true

    defaultConfig {
        applicationId "com.vodafone.global"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "0.0.1"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        // If you want to enable direct connection to BE set DIRECT
        // to true and provide protocol and host.
        // If you want to change opco and msisdn do that in
        // com.vodafone.global.sdk.RequestBuilderProvider.builder
        buildConfigField "boolean", "DIRECT", "false"
        buildConfigField "String", "DIRECT_PROTOCOL", "\"\""
        buildConfigField "String", "DIRECT_HOST", "\"\""
    }
    buildTypes {
        debug {
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-DEBUG"
        }
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        preProd {}
        prod {}
        dit {}
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            def flavorName = variant.flavorName
            // flavors can't have hyphen in their name so we have to change it
            if (flavorName == "preProd")
                flavorName = "pre-prod"
            def newName = file.name.replace("sdk-${variant.flavorName}", "vodafone-android-sdk-${flavorName}")
            if (variant.buildType.name == "release")
                newName = newName.replace("-release", "")
            output.outputFile = new File(file.parentFile, newName)
        }
    }
}

dependencies {
    compile 'com.squareup.okhttp:okhttp:2.1.0'
    compile 'com.google.guava:guava:18.0'
    compile 'joda-time:joda-time:2.4'
    debugCompile 'com.jakewharton.timber:timber:+'
}

task zip2jar(type: Zip, dependsOn: "compileDitReleaseJava") {
    from 'build/intermediates/classes/dit/release'
    destinationDir = file('build/libs')
    extension = "jar"
}

task createPom {
    apply plugin: 'maven'
    description "Generates pom.xml"
    pom {
        project {
            groupId 'com.vodafone.global'
            artifactId 'android-sdk'
            version '0.0.1-SNAPSHOT'
            packaging 'aar'
        }
    }.withXml {
        def dependenciesNode = asNode().appendNode('dependencies')

        configurations.compile.allDependencies.each { dependency ->
            def dependencyNode = dependenciesNode.appendNode('dependency')
            dependencyNode.appendNode('groupId', dependency.group)
            dependencyNode.appendNode('artifactId', dependency.name)
            dependencyNode.appendNode('version', dependency.version)
        }
    }.writeTo("$buildDir/pom.xml")
}
