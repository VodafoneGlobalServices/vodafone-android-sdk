buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/repo' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/repo' }
}

apply plugin: 'android-apt'

android {
    compileSdkVersion 19
    buildToolsVersion "20.0.0"

    defaultConfig {
        applicationId "com.vodafone.global.sdk.testapp"
        minSdkVersion 15
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    signingConfigs {
        release {
            storeFile file("vodafone-incubation.keystore")
            storePassword SEAMLESSID_TEST_APP_RELEASE_STORE_PASSWORD
            keyAlias "releasekey"
            keyPassword SEAMLESSID_TEST_APP_RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        dit {}
        preProd {}
        prod {}
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def file = output.outputFile
            def flavorName = variant.flavorName
            // flavors can't have hyphen in their name so we have to change it
            if (flavorName == "preProd")
                flavorName = "pre-prod"
            def newName = file.name.replace("test-app-${variant.flavorName}", "vodafone-android-sdk-testing-${flavorName}")
            if (variant.buildType.name == "release")
                newName = newName.replace("-release", "")
            output.outputFile = new File(file.parentFile, newName)
            if (output.zipAlign)
                output.outputFile = new File(file.parentFile, newName.replace("-unaligned", ""))
        }
    }
}

dependencies {
    ditCompile project(path: ':sdk', configuration: 'ditDebug')
    preProdCompile project(path: ':sdk', configuration: 'preProdDebug')
    prodCompile project(path: ':sdk', configuration: 'prodDebug')
    compile 'com.jakewharton:butterknife:5.1.1'
    compile 'com.jakewharton.timber:timber:2.4.1'
    apt 'net.simonvt.schematic:schematic-compiler:0.5.3'
    compile 'net.simonvt.schematic:schematic:0.5.3'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile('com.crashlytics.sdk.android:crashlytics:2.0.0@aar') {
        transitive = true;
    }
}

apt {
    arguments {
        schematicOutPackage 'com.vodafone.global.sdk.testapp.provider'
    }
}
